plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'nebula.lint' version '16.17.0'
}

group 'com.github.bibsysdev'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

gradleLint {
    rules = ['unused-dependency']
}

pmd {
    toolVersion = '6.20.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

project.ext {
    nvaCommonsVersion = '1.5.2'
    jacksonVersion = '2.12.1'
    jupiterVersion = '5.7.1'
}

dependencies {
    implementation group: 'com.github.bibsysdev', name: 'core', version: project.ext.nvaCommonsVersion
    implementation group: 'com.github.bibsysdev', name: 'apigateway', version: project.ext.nvaCommonsVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project.ext.jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: project.ext.jacksonVersion
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'

    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.ext.jupiterVersion

    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: project.ext.jacksonVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.8.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.ext.jupiterVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: project.ext.jupiterVersion
    testImplementation 'com.github.BIBSYSDEV:nva-testutils:0.1.18'
    testImplementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
}

test {
    useJUnitPlatform()
    failFast = true
    testLogging {
        events 'skipped', 'passed', 'failed'
    }
    finalizedBy jacocoTestReport
    environment "ALLOWED_ORIGIN", "*"
    environment "CRISTIN_API_HOST", "api.cristin.no"
    environment "BASE_URL", "https://api.dev.nva.aws.unit.no/person"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification